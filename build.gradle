// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.
plugins {
	// code
	id 'org.springframework.boot' version '2.2.7.RELEASE'
	id 'java'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'

    // dependency management
    id 'com.github.ben-manes.versions' version '0.28.0' // find new versions of dependency task: dependencyUpdates

    // code quality
    id "jacoco"
    id "org.owasp.dependencycheck" version "5.3.2.1"

    // IDEs
    id "eclipse"
    id "idea"
    
    // publishing
    id "maven-publish"
    id "com.cinnober.gradle.semver-git" version "2.4.0"  
    
    // docker images
    id 'com.google.cloud.tools.jib' version '2.2.0'  
}

apply plugin: 'io.spring.dependency-management'

ext {
	set('springCloudVersion', 'Hoxton.SR4')
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'eu.h2020.symbiote.ResourceAccessProxyApplication'
}

// XXX please change with your commits according to http://semver.org/
project.group = 'eu.h2020.symbiote'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// dependencies section
repositories {
	mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

// Spring related configs
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

/*
Forces dependencies' cache invalidation for each build for dependencies marked with
    { changing = true }
    e.g.
    compile('com.github.symbiote-h2020:SymbIoTeLibraries:develop-SNAPSHOT'){ changing = true }
 */
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

	exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'log4j') {
            details.useTarget "org.slf4j:log4j-over-slf4j:1.7.5"
        }
    }
}

dependencies {

    /* SymbIoTeLibraries manual:
        One can use the library from jitpack by default with the notation:
            compile('com.github.symbiote-h2020:SymbIoTeLibraries:develop-SNAPSHOT') {changing = true }
        or having cloned locally the SymbIoTeLibraries repo use it directly with project dependency by:
            compile project(':SymbIoTeLibraries')
        Important --- In order to use the latter you need to:
            1) switch comments on the artifact and project dependencies
            2) always have only one uncommented
            3) project dependency requires changes in settings.gradle file in this project
            4) never commit build.gradle which has project dependencies active as it will break CI builds
    */

    implementation('com.github.symbiote-h2020:SymbIoTeLibraries:6.0+') { changing = true }
    // localMaven
    //implementation('eu.h2020.symbiote:SymbIoTeLibraries:5.29.0-SNAPSHOT')
    //implementation('com.github.symbiote-h2020:SymbIoTeSemantics:2.3.5')
    //implementation('org.apache.httpcomponents:httpcore:4.4.11')

    // Spring
    implementation('org.springframework.cloud:spring-cloud-starter')
    implementation('org.springframework.cloud:spring-cloud-starter-config')
    implementation('org.springframework.retry:spring-retry')
    implementation('org.springframework.boot:spring-boot-starter-aop')
    //implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    implementation('org.springframework.cloud:spring-cloud-starter-zipkin')
    implementation('org.springframework.boot:spring-boot-starter-amqp')
    implementation('org.springframework.boot:spring-boot-starter-data-rest')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-data-mongodb')

	implementation('org.apache.commons:commons-lang3:3.10')
	
    // OData
    implementation("org.apache.olingo:odata-commons-api:${'4.7.1'}")
    implementation("org.apache.olingo:odata-commons-core:${'4.7.1'}")
    
    implementation("org.apache.olingo:odata-server-api:${'4.7.1'}")
    implementation("org.apache.olingo:odata-server-core:${'4.7.1'}")
  
    // Websocket
    implementation("org.springframework.boot:spring-boot-starter-websocket")

    // Others
    implementation("com.jayway.jsonpath:json-path:2.4.0")

    // test only
    implementation("org.springframework.boot:spring-boot-starter-web")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    
    // lombok
    compileOnly('org.projectlombok:lombok')
    
    //Owlapi
    //implementation('net.sourceforge.owlapi:owlapi-distribution:5.1.1')
    implementation('net.sourceforge.owlapi:owlapi-contract:5.1.14')
    implementation('net.sourceforge.owlapi:owlapi-util:3.3')    
}

// publication
bootJar {
	classifier = 'run'
}

jar {
       baseName = 'ResourceAccessProxy'
       version = project.version
}
    
task generateJavaDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = project.sourceSets.main.compileClasspath 
    destinationDir = reporting.file("javadocs")
}

task javadocJar(type: Jar, dependsOn: generateJavaDocs) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            version = project.version
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar

            artifact(file("$libsDir/$project.name-$project.version-${bootJar.classifier}.jar")) {
                classifier "run"
            }
        }
    }
}

publishToMavenLocal.dependsOn(bootJar)

// code quality below
// jacoco configuration section
jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// owasp config
dependencyCheck {
    outputDirectory = "build/reports/security"
}

// including code quality extensions into the build
check.dependsOn(jacocoTestReport) //, 'dependencyCheck')

// docker
// gradle jibDockerBuild
jib {
  from {
    //image = 'openjdk:alpine' // alpine java 8

    // 11
    //image = 'adoptopenjdk/openjdk11:jdk-11.0.3_7-alpine'
    //image = 'adoptopenjdk/openjdk11-openj9:jdk-11.0.3_7_openj9-0.14.0-alpine' // JDK
    image = 'adoptopenjdk/openjdk11-openj9:jre-11.0.3_7_openj9-0.14.0-alpine' // JRE

    // 12
    //image = 'adoptopenjdk/openjdk12-openj9:jdk-12.0.1_12_openj9-0.14.1-alpine' // JDK
    //image = 'adoptopenjdk/openjdk12-openj9:jdk-12.0.1_12_openj9-0.14.1-alpine' // JRE
  }
  
  to {
  	image = "symbioteh2020/symbiote-rap:$project.version"
  }
  
  container {
  	appRoot = '/home'
  	ports = ['8103']
  	workingDirectory = '/home'
  	jvmFlags = ['-DSPRING_BOOT_WAIT_FOR_SERVICES=symbiote_mongo:27017']

  	// uncomment for standalone mode (without security)
  	args = ['--spring.profiles.active=standalone']  	
  }  
}

task generateStandaloneDockerCompose {
    doLast {
    	String contents = new File( 'src/main/docker/rap-standalone-compose.yml').getText( 'UTF-8' )
    	contents = contents.replaceAll( /\{\{project\.version\}\}/, "$project.version" )
    	mkdir "$buildDir/docker"
    	new File( "$buildDir/docker/rap-standalone-compose.yml" ).write( contents, 'UTF-8' )
    }
}

jibDockerBuild.dependsOn(generateStandaloneDockerCompose)
